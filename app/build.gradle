apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'
def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}
def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tag'//, '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return "in Develop"
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.bigcreate.zyfw"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk { abiFilters "armeabi-v7a" }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def buildTypeName = variant.buildType.name
            println("buildType ----------->" + buildTypeName)
            def fileName = "${defaultConfig.applicationId}_${defaultConfig.versionName}_${defaultConfig.versionCode}_${buildTypeName}.apk"
            outputFileName = fileName
        }
    }
    signingConfigs {
        release {
            storeFile file(KEY_STORE_FILE)
            storePassword KEY_STORE_PASS
            keyAlias KEY_STORE_ALIAS
            keyPassword KEY_PASS
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '29.0.1'
}
final String annotation_version = "1.1.0"
final String preference_version = "1.1.0-rc01"
final String material_version = "1.1.0-alpha08"
final String navigation_ktx_version = "2.0.0"
final String lifecycle_version = "2.2.0-alpha02"
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Kotlin Library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
    //Android X Library
    implementation "androidx.appcompat:appcompat:1.1.0-rc01"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.core:core:1.2.0-alpha02"
    implementation 'androidx.core:core-ktx:1.2.0-alpha02'
    implementation "androidx.annotation:annotation:$annotation_version"
    implementation "androidx.preference:preference:$preference_version"
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta01'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha02"
    //Paging Library
    implementation "androidx.paging:paging-runtime:2.1.0"
    //LifeCycle Library
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.work:work-runtime-ktx:2.1.0"
    //Navigation Library
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_ktx_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_ktx_version"
    //Material Design Library
    implementation "com.google.android.material:material:$material_version"
    implementation 'org.litepal.android:kotlin:3.0.0'
    //Gson Library
    implementation 'com.google.code.gson:gson:2.8.5'
    //Retrofit Library
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    //OkHttp Library
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    //Tencent Map
    implementation files('libs/TencentLocationSdk_v7.1.4.3_r4aa8ac63_20180911_165428.jar')
    implementation files('libs/TencentMapSDK_Raster_v_1.3.2.jar')
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    //Boxing
    implementation 'com.bilibili:boxing-impl:1.0.4'
    //Amap
    implementation 'com.amap.api:location:latest.integration'
    implementation 'com.amap.api:search:latest.integration'
    implementation 'org.java-websocket:Java-WebSocket:1.4.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation project(':library')
}
